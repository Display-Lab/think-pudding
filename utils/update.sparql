PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX cpo: <http://example.com/cpo#>
PREFIX slowmo: <http://example.com/slowmo#>

INSERT {
  GRAPH <http://localhost:3030/ds/spek> {
    ?candi slowmo:acceptable_by ?path .
  }
}
USING <http://localhost:3030/ds/spek>
USING <http://localhost:3030/ds/seeps>
WHERE {
  ?candi a cpo:cpo_0000053 .
  ?path a cpo:causal_pathway .
  
  FILTER NOT EXISTS {
    ?path cpo:has_prerequisite ?attr .
    FILTER NOT EXISTS {
      ?candi obo:RO_0000091|slowmo:uses_intervention_property ?attr .
    }
  }
}
